<html>
 <head>
      <meta http-equiv="Content-Type" content="text/html; charset=UTF-8" />
      <title>Processing Demo</title>
      <script type="text/javascript" src="js/game.js"></script> 
 </head>
 <body>
     <h1>FLAPPY NEON CAT</h1>
     <p>Have fun playing</p>
     <script type="text/processing"> 
     int gameState, counter;
     int imx, imy, nx, ny, g, Vny;
     PImage photo, neo, ruler, topRuler;
     int[] rulerX, rulerY;

     
     void setup(){
     size(500,495);
     photo = loadImage("./images/background.PNG");
     neo = loadImage("./images/neon.png");
     ruler = loadImage("./images/ruler.png");
     topRuler = loadImage("./images/topRuler.png");
     gameState=-1;
     nx=100;
     ny=50;
     g=1;
     rulerX = new int[3];
     rulerY = new int[rulerX.length];
     for(int i = 0; i < rulerX.length; i++){
       rulerX[i] = width+200*i;
       rulerY[i] = (int)random(-270,0);
     }
     }
     
     void draw(){
      if(gameState == -1){
        startScreen();
      }
      else if (gameState==0){
        setBack();
        setRulers();
        cat();
        score();
      }
      else{
        fill(255);
        textSize(50);
        text("Game Over",115,250);
        textSize(20);
        text("click to restart",185,320);
      }
     }
     void startScreen(){
       image(photo,imx,imy);
       fill(18, 239, 255);
       textSize(40);
       text("PLAY FLAPPY NEON CAT", 20,250);
       textSize(20);
       text("Click Anywhere to Begin", 125, 290);
       if(mousePressed){
         ny = height/2;
         gameState = 0;
       }
     }
     
     void setBack(){
       image(photo,imx,imy);
       image(photo,imx + photo.width,imy);
       imx=imx-1;
       if (imx < -photo.width){
         imx = 0;
       }
     }
     void setRulers(){
      for(int i = 0; i < rulerX.length; i++){
       image(topRuler, rulerX[i], rulerY[i]);
       image(ruler, rulerX[i], rulerY[i]+500);
       rulerX[i]--;
       if (rulerX[i] < -100){
         rulerX[i] = width;
       }
       if(nx > (rulerX[i]- 40) && nx < rulerX[i] + 35 ){
         if(!(ny > rulerY[i] + 300 &&  ny < rulerY[i] + 300 + 168)){
           gameState = 1;
         }
         else if(nx == rulerX[i]){
           counter++;
         }
       }
       
     }
     }
     void cat(){
       image(neo,nx,ny);
       ny = ny + Vny;
       Vny = Vny + g;
       if(ny > height || ny < 0){
         textSize(30);
         text("NEON CAT fell off the screen!", 35, 290);
         gameState = 1;
       }
     }
     void score(){
       fill(255);
       textSize(20);
       text("Score:"+ counter, width - 120,40);
     }
     void mousePressed(){
       Vny = -15;
     }
     void mouseClicked(){
       if(gameState==1){
         setup();
         counter=0;
       }
     }
</script>
     <canvas id="sketch" style="border: 1px solid black;"></canvas>
 </body>
</html>